# TITLE
message("\n*--------*")
message("| MACROS |")
message("*--------*")

# >----------< MACROS >--------------------------------------------------------------------------< #

macro(PRINT_HELLO_WORLD)
  message("\n[Macro | PRINT_HELLO_WORLD()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  Hello, World!")
endmacro()

macro(PRINT_HELLO SUBJECT)
  message("\n[Macro | PRINT_HELLO(SUBJECT)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  Hello, ${SUBJECT}!")
endmacro()

macro(LOG LEVEL MESSAGE)
  message("\n[Macro | LOG(LEVEL MESSAGE)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  [${LEVEL}] | ${MESSAGE}")
  if (${ARGC} GREATER_EQUAL 3)
    message("  [EXTRA] | ${ARGV2}")
  endif()
endmacro()

macro(SHOP)
  message("\n[Macro | shop()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  set(NO_VALUE_ARGS "NOW")
  set(ONE_VALUE_ARGS "PAY_WITH")
  set(MULTI_VALUE_ARGS "THINGS")
  cmake_parse_arguments(
    "ARG"
    "${NO_VALUE_ARGS}"
    "${ONE_VALUE_ARGS}"
    "${MULTI_VALUE_ARGS}"
    ${ARGN}
  )
  if(ARG_NOW)
    message("  Going shopping now!")
  else()
    message("  Going shopping later!")
  endif()
  if(DEFINED ARG_PAY_WITH)
    message("  Paying with: ${ARG_PAY_WITH}")
  else()
    message("  Paying with: cash")
  endif()
  if(DEFINED ARG_THINGS)
    message("  Buying things:")
    foreach(THING IN ITEMS ${ARG_THINGS})
      message("    - ${THING}")
    endforeach()
  else()
    message("  Buying things: nothing")
  endif()
endmacro()

macro(PRINT_VARIABLE VARIABLE)
  message("\n[Macro | PRINT_VARIABLE(VARIABLE)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  VARIABLE: ${${VARIABLE}}")
endmacro()

macro(CREATE_DICE LST_DICE)
  message("\nBefore calling CREATE_DICE(): CH09_MAC_LST_DICE = ${CH09_MAC_LST_DICE}")
  message("[Macro | create_dice(LST_DICE)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  set(${LST_DICE} 1 2 3 4 5 6)
endmacro()

macro(MODIFY_DICE)
  message("\nBefore calling modify_dice(): CH09_MAC_LST_DICE = ${CH09_MAC_LST_DICE}")
  message("[Macro | modify_dice()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  set(CH09_MAC_LST_DICE 6 5 4 3 2 1)
endmacro()

macro(inner_macro)
  message("\n[Macro | inner_macro()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
endmacro()

macro(outer_macro)
  message("\n[Macro | outer_macro()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  string(REPLACE ";" "]==] [==[" ARGS "[==[${ARGV}]==]")
  message("  Forwarding arguments: ${ARGS}")
  cmake_language(EVAL CODE "inner_macro(${ARGS})")
endmacro()


# >----------< CALLS >---------------------------------------------------------------------------< #

PRINT_HELLO_WORLD()
PRINT_HELLO("CMake")
LOG("ERROR" "Something went wrong!")
LOG("ERROR" "Some other thing went wrong!" "Some extra information.")
SHOP(NOW PAY_WITH "debit card" THINGS "apple" "banana" "cherry")
SHOP(PAY_WITH "credit card" THINGS "grapes" "kiwi" "lemon")

set(CH09_MAC_VAR "value")
PRINT_VARIABLE(CH09_MAC_VAR)

CREATE_DICE(CH09_MAC_LST_DICE)
message("After calling CREATE_DICE(): CH09_MAC_LST_DICE = ${CH09_MAC_LST_DICE}")

MODIFY_DICE()
message("After calling MODIFY_DICE(): CH09_MAC_LST_DICE = ${CH09_MAC_LST_DICE}")

outer_macro("a;b;c" "1;2;3" "x;y;z")
