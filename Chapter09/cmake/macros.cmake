# TITLE
message("\n*--------*")
message("| MACROS |")
message("*--------*")

# >----------< MACROS >--------------------------------------------------------------------------< #

macro(PRINT_HELLO_WORLD)
  message("\n[Macro | PRINT_HELLO_WORLD()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  Hello, World!")
endmacro()

macro(PRINT_HELLO subject)
  message("\n[Macro | PRINT_HELLO(subject)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  Hello, ${subject}!")
endmacro()

macro(LOG level message)
  message("\n[Macro | LOG(level message)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  [${level}] | ${message}")
  if (${ARGC} GREATER_EQUAL 3)
    message("  [EXTRA] | ${ARGV2}")
  endif()
endmacro()

macro(SHOP)
  message("\n[Macro | shop()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  set(noValueArgs "NOW")
  set(oneValueArgs "PAY_WITH")
  set(multiValueArgs "THINGS")
  cmake_parse_arguments(
    "ARG"
    "${noValueArgs}"
    "${oneValueArgs}"
    "${multiValueArgs}"
    ${ARGN}
  )
  if(ARG_NOW)
    message("  Going shopping now!")
  else()
    message("  Going shopping later!")
  endif()
  if(DEFINED ARG_PAY_WITH)
    message("  Paying with: ${ARG_PAY_WITH}")
  else()
    message("  Paying with: cash")
  endif()
  if(DEFINED ARG_THINGS)
    message("  Buying things:")
    foreach(thing IN ITEMS ${ARG_THINGS})
      message("    - ${thing}")
    endforeach()
  else()
    message("  Buying things: nothing")
  endif()
endmacro()

macro(PRINT_VARIABLE variable)
  message("\n[Macro | PRINT_VARIABLE(variable)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  message("  variable: ${${variable}}")
endmacro()

macro(CREATE_DICE variableName)
  message("\nBefore calling CREATE_DICE(): mcr_dice = ${mcr_dice}")
  message("[Macro | create_dice(variableName)]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  set(${variableName} 1 2 3 4 5 6)
endmacro()

macro(MODIFY_DICE)
  message("\nBefore calling modify_dice(): mcr_dice = ${mcr_dice}")
  message("[Macro | modify_dice()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  set(mcr_dice 6 5 4 3 2 1)
endmacro()

macro(INNER_MACRO)
  message("\n[Macro | INNER_MACRO()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
endmacro()

macro(OUTER_MACRO)
  message("\n[Macro | OUTER_MACRO()]")
  message("  Arguments:")
  message("    ARGC = ${ARGC}")
  message("    ARGV = ${ARGV}")
  message("    ARGN = ${ARGN}")
  string(REPLACE ";" "]==] [==[" args "[==[${ARGV}]==]")
  message("  Forwarding arguments: ${args}")
  cmake_language(EVAL CODE "INNER_MACRO(${args})")
endmacro()


# >----------< CALLS >---------------------------------------------------------------------------< #

PRINT_HELLO_WORLD()
PRINT_HELLO("CMake")
LOG("ERROR" "Something went wrong!")
LOG("ERROR" "Some other thing went wrong!" "Some extra information.")
SHOP(NOW PAY_WITH "debit card" THINGS "apple" "banana" "cherry")
SHOP(PAY_WITH "credit card" THINGS "grapes" "kiwi" "lemon")

set(mcr_var "value")
PRINT_VARIABLE(mcr_var)

CREATE_DICE("mcr_dice")
message("After calling CREATE_DICE(): mcr_dice = ${mcr_dice}")

MODIFY_DICE()
message("After calling MODIFY_DICE(): mcr_dice = ${mcr_dice}")

OUTER_MACRO("a;b;c" "1;2;3" "x;y;z")
